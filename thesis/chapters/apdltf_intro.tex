The syntax of the APDL language is very simple, and the goal of this document is to
provide a small introduction to APDL.

\section*{Creating a project}

A project is always identified by a name through the \apdlinline{project_name}
key-value pair. We could also fragment the project into multiple files and
include them with the \apdlinline{@include} keyword. So, an empty project looks
like :

\begin{apdlcode}
project_name = "Example"

@include "apdl_component.apdl"
@include "other_file.apdl"
\end{apdlcode}

We also need to note that the comments aren't supported for the moment.

\section*{Defining a device}

A device is the key entity of an APDL project, and it is defined by using the
\apdlinline{@device} keyword with an identifier. The identifier has to be unique
and don't containing any special character. The full grammar of APDL is available
in appendix \ref{app:apdl_ebnf_diagramm}.

When we define a device, we have to specify an ``id'' and a ``framework'' :
\begin{apdlcode}
@device example {
  id = Uno
  framework = arduino
}
\end{apdlcode}

For the moment, only ``arduino'' and ``mbed'' are supported for the framework
value. The ``id'' value corresponds to the one available with
PlatformIO.~\footnote{\url{http://platformio.org/boards}}. In addition to these
two values, we could define inputs with the \apdlinline{@input} keyword and
serial with the \apdlinline{@serial} keyword.

A serial is always defined in the same way :
\begin{apdlcode}
@serial inputIdentifier samplingType
\end{apdlcode}

The ``inputIdentifier'' corresponds to the identifier of a previously defined
input in the same device. The ``samplingType'' corresponds to the kind of
sampling to use and its potential value. For the moment, there are two kinds of
sampling :

\begin{itemize}
\item by update : \apdlinline{@serial inputIdentifier update}.
\item by frequency : \apdlinline{@serial inputIdentifier each n timeUnit}, where
  $n$ is an integer and $timeUnit$ is a time unit.
\end{itemize}

The time units are corresponding like the following map :
\begin{itemize}
\item \apdlinline{ns} : nanoseconds
\item \apdlinline{ms} : milliseconds
\item \apdlinline{s} : seconds
\item \apdlinline{m} : minutes
\item \apdlinline{h} : hours
\item \apdlinline{d} : days
\end{itemize}

The definition of an input for a device is described in the following sections.

\section*{Defining new inputs}

Defining new inputs is available through the \apdlinline{@define input} keyword.
The input needs a unique identifier and a parameter list. The list could be
empty and the parameter is written with the pattern \apdlinline{identifier :
  type}. The type is the following :
\begin{itemize}
\item \apdlinline{str} : a unique string, without space.
\item \apdlinline{id} : an identifier.
\item The other types : int, float, long, bool, double, char, byte and short,
  have the same behaviour as in the C language.
\end{itemize}

Once the input signature has been defined, we have to specify the
\apdlinline{@gen} part. This part owns five key-value pairs that are always in
the same order and are always required by the compiler. Except of the ``type''
value, the others are literal strings that are going to be generated inside the
source code of the target. So the code written in those values are specific to
the target language or framework. For example, the analogue input for arduino and
mbed is defined with the following :
\begin{apdlcode}
@define input analogInput pin:str {
    @gen mbed {
        global = "AnalogIn @id(@pin);"
        setup = ""
        loop = ""
        expr = "@id.read()"
        type = float
    }
    @gen arduino {
        global = ""
        setup = ""
        loop = ""
        expr = "analogRead(@pin)"
        type = int
    }
}
\end{apdlcode}

The four fields are :
\begin{itemize}
\item ``global'' : printed inside the global definition.
\item ``setup'' : printed inside the setup definition.
\item ``loop'' : printed inside the loop definition.
\item ``expr'' : the expression inside the target source code which provides the value.
\end{itemize}

For example, the location of those fields are like the following for Arduino :
\begin{arduinocode}
// Global definition

void setup() {
  // Setup
}

void loop() {
  // Loop

  // Expr and type
  /* type */ data = /* expr */;
}
\end{arduinocode}

or for Mbed :
\begin{cppcode}
// Global definition

int main() {
  // Setup

  while(1) {
    // Loop
    
    // Expr and type
    /* type */ data = /* expr */;
  }
  
}
\end{cppcode}

We need to be careful when defining global variable, because the identifier
collisions are not verified.

To use a defined input, we just write the type of the input after its identifier
and provide the required number of arguments after the type :
\begin{apdlcode}
@input temp analogInput 1
\end{apdlcode}

\section*{Defining new components}

Defining new components is almost the same as defining new inputs, except that
the \apdlinline{@gen} block don't have a ``type'' key-value pair but two
additional information for the components itself, the input's identifier and
type, and the output's type :

\begin{apdlcode}
@define component simpleOperator op:str {
    @in x:int y:int
    @out int
    @gen mbed {
        global = ""
        setup = ""
        loop = ""
        expr = "@x @op @y"
    }
    @gen arduino {
        global = ""
        setup = ""
        loop = ""
        expr = "@x @op @y"
    }
}

@define component loopCounter {
    @in
    @out int
    @gen arduino {
        global = "int a;"
        setup = "a = 0;"
        loop = "a = a + 1;"
        expr = "a"
  }
}
\end{apdlcode}

A component is generated as a function for the moment, so we can't have
a component with multiple outputs.

The usage of a component is almost the same as for the input, but the arguments
of the component come before the inputs arguments :
\begin{apdlcode}
@input componentedTemp simpleOperator + inputA inputB
\end{apdlcode}

\section*{Defining new transformations} 

Defining new transformations is quite different from the definition of new
inputs or components. To define new transformations, we use an additional DSL called TF,
for TransForm Language. The TF language is very similar to Scala and acts as a
high-level language.

The complete grammar of the TF language is available in appendix
\ref{app:apdl_ebnf_diagramm}. The major differences with Scala are :
\begin{itemize}
\item The types are written with lowercase letters.
\item The types are obligatory, there is no type inference.
\item The function definition output type is given after \apdlinline{->} instead
  of \apdlinline{:}.
\item Type casting is available with the C syntax : \apdlinline{(int)3.5}
\item The \apdlinline{return} keyword is required for a non-void function.
\end{itemize}

Here is an example of the definition of a transformation function :
\begin{apdlcode}
@define transform def tf (x:int) -> float {
    val B : int = 3975
    val resistance : float = (float)(1023 - x) * 1000 / x
    val temperature : float = 1 / (log(resistance/1000) / B + 1 / 298.15) - 273.15
    return temperature
}
\end{apdlcode}